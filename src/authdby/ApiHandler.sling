
/*
 * This file is part of Sympathy
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018 Eqela Oy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License, version 3,
 * as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

class #webapi:

import sympathy
import capex.data
import capex.crypto

macro INIT_SYMPATHY_APIGWY_AUTHORIZED_REQUEST
{
	var userid = req.getHeader("x-sympathy-apigwy-userid")
	assert String.isNotEmpty(userid):
		req.sendJSONError(ServerError.notAllowed())
	var sessionid = req.getHeader("x-sympathy-apigwy-sessionid")
	assert String.isNotEmpty(sessionid):
		req.sendJSONError(ServerError.notAllowed())
}

prop allowRegistration = true
prop db as SQLDatabase
prop validationEmailTemplate as string
prop recoveryEmailTemplate as string
prop smtpSender as SMTPSender
var sha as SHAEncoder

// TODO / FIXME: Maintenance routines to delete old/expired records from the tables

func shaEncode(str as string) private as string
{
	assert str
	if not sha:
		sha = SHAEncoder.create()
	assert sha
	return sha.encodeAsString(String.toUTF8Buffer(str), SHAEncoder.SHA1)
}

func hashPassword(password as string) as string:
	return shaEncode(password)

func getUserByAddress(address as string, cb as function<void,UserModel>) private
{
	assert String.isNotEmpty(address):
		cb(null)
	var stmt = assert db.prepare("SELECT auth_user.* FROM auth_user_address LEFT JOIN auth_user ON auth_user_address.userid=auth_user.id WHERE auth_user_address.address = ?"):
		cb(null)
	stmt.addParamString(address)
	db.querySingleRow(stmt, func(v as DynamicMap) {
		var user = assert UserModel.forJsonObject(v):
			cb(null)
		cb(user)
	})
}

func getUserById(id as string, cb as function<void,UserModel>) private
{
	assert String.isNotEmpty(id):
		cb(null)
	var stmt = assert db.prepareSelectStatement("auth_user", new DynamicMap().set("id", id)):
		cb(null)
	db.querySingleRow(stmt, func(v as DynamicMap) {
		var user = assert UserModel.forJsonObject(v):
			cb(null)
		cb(user)
	})
}

func checkCredentials(address as string, password as string, cb as function<void,string>) private
{
	assert String.isNotEmpty(password):
		cb(null)
	var pwhash = assert hashPassword(password):
		cb(null)
	getUserByAddress(address, func(user as UserModel) {
		assert user:
			cb(null)
		if String.equals(pwhash, user.getPassword()) {
			cb(user.getId())
		}
		else {
			cb(null)
		}
	})
}

func generateSessionId(userid as string) private as string
{
	assert userid
	var str = userid .. String.forLongInteger(SystemClock.asSeconds()) .. String.forInteger(Random.getSharedInstance().nextInt())
	return shaEncode(str)
}

func createSession(userid as string, cb as function<void,string>) private
{
	var sid = assert generateSessionId(userid):
		cb(null)
	var session = new SessionModel()
	session.setId(sid)
	session.setCreated(SystemClock.asUTCSeconds())
	session.setAccessed(SystemClock.asUTCSeconds())
	session.setUserid(userid)
	var stmt = assert db.prepareInsertStatement("auth_session", session.toJsonObject() as DynamicMap):
		cb(null)
	db.execute(stmt, func(v as bool) {
		if v == false {
			cb(null)
		}
		else {
			cb(sid)
		}
	})
}

func getSession(sessionid as string, cb as function<void,SessionModel>) private
{
	var stmt = assert db.prepareSelectStatement("auth_session", new DynamicMap().set("id", sessionid)):
		cb(null)
	db.querySingleRow(stmt, func(v as DynamicMap) {
		var session = assert SessionModel.forJsonObject(v):
			cb(null)
		cb(session)
	})
}

func generateValidationSecret private as string
{
	var rnd = Random.getSharedInstance()
	var sb = new StringBuilder()
	for(var n=0; n<4; n++):
		sb.append(String.forInteger(rnd.nextInt(10)))
	return sb.toString()
}

func isAddressFormatValid(address as string) private as bool
{
	assert String.isNotEmpty(address)
	var at = String.getIndexOf(address, '@')
	assert at > 0
	var rdot = String.getLastIndexOf(address, '.')
	assert rdot > 0
	assert rdot > at
	return true
}

func sendValidationSecret(secret as string, address as string) private as bool
{
	assert smtpSender
	assert validationEmailTemplate
	assert secret
	assert address
	var data = new DynamicMap()
	data.set("secret", secret)
	var msg = SMTPMessage.forTemplate(validationEmailTemplate, data)
	msg.addTo(address)
	smtpSender.send(msg, null)
	return true
}

func sendRecoverySecret(secret as string, address as string) private as bool
{
	assert smtpSender
	assert recoveryEmailTemplate
	assert secret
	assert address
	var data = new DynamicMap()
	data.set("secret", secret)
	var msg = SMTPMessage.forTemplate(recoveryEmailTemplate, data)
	msg.addTo(address)
	smtpSender.send(msg, null)
	return true
}

func getValidationModel(address as string, secret as string, cb as function<void,ValidationModel>) private
{
	assert String.isNotEmpty(address) && String.isNotEmpty(secret):
		cb(null)
	var stmt = assert db.prepareSelectStatement("auth_validation", new DynamicMap().set("address", address).set("secret", secret)):
		cb(null)
	db.querySingleRow(stmt, func(v as DynamicMap) {
		var validation = assert ValidationModel.forJsonObject(v):
			cb(null)
		cb(validation)
	})
}

func checkAddressAvailability(address as string, cb as function<void,bool>) private
{
	var stmt = assert db.prepareSelectStatement("auth_user_address", new DynamicMap().set("address", address)):
		cb(false)
	db.querySingleRow(stmt, func(v as DynamicMap) {
		if v {
			cb(false)
		}
		else {
			cb(true)
		}
	})
}

func generateUserId private as string
{
	var str = "user" .. String.forLongInteger(SystemClock.asSeconds()) .. String.forInteger(Random.getSharedInstance().nextInt())
	return shaEncode(str)
}

func createNewUserRecordSync private as UserModel
{
	for(var n=0; n<16; n++) {
		var user = new UserModel()
		user.setId(generateUserId())
		user.setCreated(SystemClock.asUTCSeconds())
		user.setPassword(null)
		var stmt = assert db.prepareInsertStatement("auth_user", user.toJsonObject() as DynamicMap)
		if db.execute(stmt):
			return user
	}
	return null
}

func deleteUserRecordSync(userid as string) private as bool
{
	assert userid
	var stmt = assert db.prepareDeleteStatement("auth_user", new DynamicMap().set("id", userid))
	return db.execute(stmt)
}

func createNewUserRecordWithAddress(address as string, cb as function<void,string>) private
{
	// NOTE: There is a SLIGHT chance here, if all the stars align correctly, and many very unlikely
	// events happen at the same time, in an exact sequence, that we end up with a stale user record
	// that has no address. Even this could be resolved through the use of a transaction instead of
	// trying to delete the record after (FIXME).
	var record = assert createNewUserRecordSync():
		cb(null)
	var userid = record.getId()
	addAddressToUser(userid, address, func(v as bool) {
		if v {
			cb(userid)
		}
		else {
			deleteUserRecordSync(userid)
			cb(null)
		}
	})
}

func addAddressToUser(userid as string, address as string, cb as function<void,bool>) private
{
	assert userid && address:
		cb(false)
	var uam = new UserAddressModel()
	uam.setAddress(address)
	uam.setUserid(userid)
	uam.setCreated(SystemClock.asUTCSeconds())
	var stmt = assert db.prepareInsertStatement("auth_user_address", uam.toJsonObject() as DynamicMap):
		cb(false)
	db.execute(stmt, cb)
}

func getAllUserAddresses(userid as string, cb as function<void, vector<UserAddressModel>>) private
{
	var stmt = assert db.prepareSelectStatement("auth_user_address", new DynamicMap().set("userid", userid)):
		cb(null)
	db.query(stmt, func(rs as SQLResultSetIterator) {
		var v = new vector<UserAddressModel>
		loop {
			var r = rs.next()
			if not r:
				break
			var model = UserAddressModel.forJsonObject(r)
			if not model:
				continue
			v += model
		}
		cb(v)
	})
}

func canDeleteUserAddress(userid as string, address as string, cb as function<void, bool>) private
{
	getAllUserAddresses(userid, func(addresses as vector<UserAddressModel>) {
		if addresses == null || Vector.getSize(addresses) < 2 {
			cb(false)
			return
		}
		foreach add in addresses {
			if String.equals(add.getAddress(), address) {
				cb(true)
				return
			}
		}
		cb(false)
	})
}

func isValidPassword(password as string) private as bool
{
	assert String.isNotEmpty(password)
	assert String.getLength(password) > 4
	return true
}

func getRecoveryUserid(address as string, secret as string, cb as function<void,string>) private
{
	assert address && secret:
		cb(null)
	var criteria = new DynamicMap().set("address", address).set("secret", secret)
	var stmt = assert db.prepareSelectStatement("auth_recovery", criteria):
		cb(null)
	db.querySingleRow(stmt, func(v as DynamicMap) {
		assert v:
			cb(null)
		var recovery = assert RecoveryModel.forJsonObject(v):
			cb(null)
		cb(recovery.getUserid())
	})
}

POST "createSessionWithCredentials"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	var password = params.getString("password")
	checkCredentials(address, password, func(userid as string) {
		if String.isEmpty(userid) {
			req.sendJSONError(ServerError.invalidCredentials())
			return
		}
		createSession(userid, func(sessionid as string) {
			if String.isEmpty(sessionid) {
				req.sendJSONError(ServerError.failedToSave())
				return
			}
			req.sendJSONOK(DynamicMap.forObjectMap({
				"sessionid" : sessionid
			}))
		})
	})
}

POST "deleteSession"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var sessionid = params.getString("sessionid")
	var stmt = assert db.prepareDeleteStatement("auth_session", new DynamicMap().set("id", sessionid)):
		req.sendJSONError(ServerError.internalError())
	db.execute(stmt, func(v as bool) {
		if v == false {
			req.sendJSONError(ServerError.failedToSave())
		}
		else {
			req.sendJSONOK()
		}
	})
}

POST "deleteUserSession"
{
	INIT_SYMPATHY_APIGWY_AUTHORIZED_REQUEST()
	var stmt = assert db.prepareDeleteStatement("auth_session", new DynamicMap().set("id", sessionid)):
		req.sendJSONError(ServerError.internalError())
	db.execute(stmt, func(v as bool) {
		if v == false {
			req.sendJSONError(ServerError.failedToSave())
		}
		else {
			req.sendJSONOK()
		}
	})
}

POST "registerUser"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	assert allowRegistration:
		req.sendJSONError(ServerError.notAllowed())
	assert address:
		req.sendJSONError(ServerError.invalidRequest())
	assert isAddressFormatValid(address):
		req.sendJSONError(ServerError.invalidInputFormat())
	checkAddressAvailability(address, func(available as bool) {
		assert available:
			req.sendJSONError(ServerError.alreadyInUse())
		var secret = generateValidationSecret()
		assert sendValidationSecret(secret, address):
			req.sendJSONError(ServerError.internalError())
		var validation = new ValidationModel()
		validation.setAddress(address)
		validation.setSecret(secret)
		validation.setUserid(null)
		validation.setCreated(SystemClock.asUTCSeconds())
		var stmt = assert db.prepareInsertStatement("auth_validation", validation.toJsonObject() as DynamicMap):
			req.sendJSONError(ServerError.internalError())
		db.execute(stmt, func(v as bool) {
			if v == false {
				req.sendJSONError(ServerError.failedToSave())
			}
			else {
				req.sendJSONOK()
			}
		})
	})
}

POST "validateAddress"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	var secret = params.getString("secret")
	assert String.isNotEmpty(address) && String.isNotEmpty(secret):
		req.sendJSONError(ServerError.invalidRequest())
	getValidationModel(address, secret, func(validation as ValidationModel) {
		assert validation:
			req.sendJSONError(ServerError.notFound())
		var stmt = assert db.prepareDeleteStatement("auth_validation", new DynamicMap().set("address", address).set("secret", secret)):
			req.sendJSONError(ServerError.internalError())
		db.executeUpdateDelete(stmt, func(records as int) {
			if records < 1 {
				// no records were deleted. that means that somebody deleted the record
				// between here and when we just queried it. yes, it happens...
				req.sendJSONError(ServerError.notFound())
			}
			else {
				checkAddressAvailability(address, func(available as bool) {
					assert available:
						req.sendJSONError(ServerError.alreadyInUse())
					var userid = validation.getUserid()
					if String.isEmpty(userid) {
						createNewUserRecordWithAddress(address, func(userid as string) {
							assert userid:
								req.sendJSONError(ServerError.failedToSave())
							createSession(userid, func(sessionid as string) {
								assert sessionid:
									req.sendJSONError(ServerError.internalError())
								req.sendJSONOK(DynamicMap.forObjectMap({
									"sessionid" : sessionid
								}))
							})
						})
					}
					else {
						addAddressToUser(userid, address, func(v as bool) {
							assert v:
								req.sendJSONError(ServerError.failedToSave())
							req.sendJSONOK()
						})
					}
				})
			}
		})
	})
}

POST "addUserAddress"
{
	INIT_SYMPATHY_APIGWY_AUTHORIZED_REQUEST()
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	assert address:
		req.sendJSONError(ServerError.invalidRequest())
	assert isAddressFormatValid(address):
		req.sendJSONError(ServerError.invalidInputFormat())
	getSession(sessionid, func(session as SessionModel) {
		assert session:
			req.sendJSONError(ServerError.invalidSession())
		checkAddressAvailability(address, func(available as bool) {
			assert available:
				req.sendJSONError(ServerError.alreadyInUse())
			var secret = generateValidationSecret()
			assert sendValidationSecret(secret, address):
				req.sendJSONError(ServerError.internalError())
			var validation = new ValidationModel()
			validation.setAddress(address)
			validation.setSecret(secret)
			validation.setUserid(session.getUserid())
			validation.setCreated(SystemClock.asUTCSeconds())
			var stmt = assert db.prepareInsertStatement("auth_validation", validation.toJsonObject() as DynamicMap):
				req.sendJSONError(ServerError.internalError())
			db.execute(stmt, func(v as bool) {
				if v == false {
					req.sendJSONError(ServerError.failedToSave())
				}
				else {
					req.sendJSONOK()
				}
			})
		})
	})
}

POST "deleteUserAddress"
{
	INIT_SYMPATHY_APIGWY_AUTHORIZED_REQUEST()
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	assert address:
		req.sendJSONError(ServerError.invalidRequest())
	assert isAddressFormatValid(address):
		req.sendJSONError(ServerError.invalidInputFormat())
	getSession(sessionid, func(session as SessionModel) {
		assert session:
			req.sendJSONError(ServerError.invalidSession())
		var userid = session.getUserid()
		// NOTE: If the user has two addresses and they are both being deleted simultaneously,
		// there is a chance that the user account ends up not having any address. (FIXME)
		// Solve with a transaction and rollback.
		canDeleteUserAddress(userid, address, func(canDelete as bool) {
			assert canDelete:
				req.sendJSONError(ServerError.notAllowed())
			var stmt = assert db.prepareDeleteStatement("auth_user_address", new DynamicMap().set("userid", userid).set("address", address)):
				req.sendJSONError(ServerError.internalError())
			db.executeUpdateDelete(stmt, func(v as int) {
				if v < 1 {
					req.sendJSONError(ServerError.failedToSave())
				}
				else {
					req.sendJSONOK()
				}
			})
		})
	})
}

POST "changePassword"
{
	INIT_SYMPATHY_APIGWY_AUTHORIZED_REQUEST()
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var oldPassword = params.getString("oldPassword")
	var newPassword = params.getString("newPassword")
	getSession(sessionid, func(session as SessionModel) {
		assert session:
			req.sendJSONError(ServerError.invalidSession())
		var userid = session.getUserid()
		getUserById(userid, func(user as UserModel) {
			assert user:
				req.sendJSONError(ServerError.invalidSession())
			var actualPassword = user.getPassword()
			if String.isEmpty(oldPassword) && String.isEmpty(actualPassword) {
				; // this is ok
			}
			else {
				assert String.equals(hashPassword(oldPassword), actualPassword):
					req.sendJSONError(ServerError.invalidCredentials())
			}
			assert isValidPassword(newPassword):
				req.sendJSONError(ServerError.invalidInputFormat())
			var criteria = new DynamicMap().set("id", userid)
			var data = new DynamicMap().set("password", hashPassword(newPassword))
			var stmt = assert db.prepareUpdateStatement("auth_user", criteria, data):
				req.sendJSONError(ServerError.internalError())
			db.executeUpdateDelete(stmt, func(v as int) {
				if v < 1 {
					req.sendJSONError(ServerError.failedToSave())
				}
				else {
					req.sendJSONOK()
				}
			})
		})
	})
}

POST "startResetPassword"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	assert address:
		req.sendJSONError(ServerError.invalidRequest())
	getUserByAddress(address, func(user as UserModel) {
		assert user:
			req.sendJSONError(ServerError.notFound())
		var secret = generateValidationSecret()
		assert sendRecoverySecret(secret, address):
			req.sendJSONError(ServerError.internalError())
		var recovery = new RecoveryModel()
		recovery.setAddress(address)
		recovery.setSecret(secret)
		recovery.setUserid(user.getId())
		recovery.setCreated(SystemClock.asUTCSeconds())
		var stmt = assert db.prepareInsertStatement("auth_recovery", recovery.toJsonObject() as DynamicMap):
			req.sendJSONError(ServerError.internalError())
		db.execute(stmt, func(v as bool) {
			if v == false {
				req.sendJSONError(ServerError.failedToSave())
			}
			else {
				req.sendJSONOK()
			}
		})
	})
}

POST "resetPassword"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var address = params.getString("address")
	var secret = params.getString("secret")
	var newPassword = params.getString("newPassword")
	assert isValidPassword(newPassword):
		req.sendJSONError(ServerError.invalidInputFormat())
	getRecoveryUserid(address, secret, func(userid as string) {
		assert userid:
			req.sendJSONError(ServerError.invalidCredentials())
		var todelete = new DynamicMap().set("address", address).set("secret", secret)
		var stmt = assert db.prepareDeleteStatement("auth_recovery", todelete):
			req.sendJSONError(ServerError.internalError())
		db.executeUpdateDelete(stmt, func(records as int) {
			assert records > 0:
				req.sendJSONError(ServerError.invalidCredentials())
			var criteria = new DynamicMap().set("id", userid)
			var data = new DynamicMap().set("password", hashPassword(newPassword))
			var stmt = assert db.prepareUpdateStatement("auth_user", criteria, data):
				req.sendJSONError(ServerError.internalError())
			db.executeUpdateDelete(stmt, func(v as int) {
				if v < 1 {
					req.sendJSONError(ServerError.failedToSave())
				}
				else {
					req.sendJSONOK()
				}
			})
		})
	})
}

POST "checkSession"
{
	var params = assert req.getBodyJSONMap():
		req.sendJSONError(ServerError.invalidRequest())
	var sessionid = params.getString("sessionid")
	assert sessionid:
		req.sendJSONError(ServerError.invalidRequest())
	var stmt = assert db.prepareSelectStatement("auth_session", new DynamicMap().set("id", sessionid)):
		req.sendJSONError(ServerError.internalError())
	db.querySingleRow(stmt, func(v as DynamicMap) {
		assert v:
			req.sendJSONError(ServerError.notFound())
		var userid = assert v.getString("userid"):
			req.sendJSONError(ServerError.notFound())
		req.sendJSONOK(DynamicMap.forObjectMap({
			"userid" : userid
		}))
	})
}
